# Retrives SuperPokeBall, PokeBall with a weight greater than 100
# DL
PokeBall and weight some xsd:decimal[ > 100 ]

# Retrives SuperPokeBall, PokeBall with a weight greater than 100
# SPARQL
SELECT DISTINCT ?pokeball 
WHERE {
    ?pokeball rdf:type ?type .
    ?type rdfs:subClassOf* pokeOntology:PokeBall .
    ?pokeball pokeOntology:weight ?w .
    FILTER(?w > "100"^^xsd:float) .
}
----------
# DL
# Retrives all the humans possessing a pokeball which contains a certain pokemon
Human and possess some (PokemonItem and contain some Pokemon)

# Retrives all the Human and, optionally, his Pokemon: a human possessing a pokeball which contains a certain pokemon has that pokemon
# SPARQL
SELECT ?human ?pokemon
WHERE {
    ?human rdf:type ?type .
    ?type rdfs:subClassOf* pokeOntology:Human .
    OPTIONAL {
        ?human pokeOntology:possess/pokeOntology:contain ?pokemon .
    }
}
----------
# Retrives BeginnerPokemonTrainer, Human that possesses a PokeBall that contains a StarterPokemon and has an age less than 12
# DL
Human and possess some (PokemonItem and contain some StarterPokemon) and age some xsd:decimal[ < 12 ]

# Retrives BeginnerPokemonTrainer, Human that possesses a PokeBall that contains a StarterPokemon and has an age less than 12
# SPARQL
SELECT DISTINCT ?human
WHERE {
    ?human rdf:type ?type .
    ?type rdfs:subClassOf* pokeOntology:Human .
    ?human pokeOntology:possess ?pokeball .
    ?pokeball pokeOntology:contain ?pokemon .
    ?pokemon rdf:type pokeOntology:StarterPokemon .
    ?human pokeOntology:age ?a .
    FILTER(?a < "12"^^xsd:int) .
}
----------
# Retrives all the Pokemon which does not have an evolution
# DL
???

# Retrives all the Pokemon which does not have an evolution
# SPARQL
SELECT ?pokemon
WHERE {
    ?pokemon rdf:type ?type .
    ?type rdfs:subClassOf* pokeOntology:Pokemon .
    FILTER NOT EXISTS {
        ?pokemon pokeOntology:evolves ?evolution .
    }
}
